buildscript {
    repositories {
        maven { url "https://artifactory.intranet.opt/artifactory/gradle-virtual/" }
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:6.0.3'
    }
}


plugins {
    id "org.sonarqube" version "2.8"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'
apply plugin: 'org.owasp.dependencycheck'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar.baseName = projectName

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName = projectName
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

javadoc {
    options.charSet = 'UTF-8'
}

jacoco {
    toolVersion = '0.8.6'
}

sonarqube {
    properties {
        property 'sonar.projectName', projectName
        property 'sonar.cpd.exclusions', '**/src/test/**'
        property 'sonar.coverage.exclusions', '**/config/**,**/src/test/**'
        property 'sonar.exclusions', '**/Application.java'
    }
}

repositories {
    mavenLocal()
    maven { url "https://artifactory.intranet.opt/artifactory/gradle-virtual/" }
}

configurations.all {
    resolutionStrategy {
        force 'com.google.guava:guava:19.0'
    }
}

dependencies {
    implementation 'net.logstash.logback:logstash-logback-encoder:6.6'
    implementation 'org.springframework:spring-context:5.3.6'
    implementation 'org.openjdk.jol:jol-core:0.15'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.3.5.RELEASE'
}

test {
    useJUnitPlatform()
}

task createPom {
    pom {
        project {
            artifactId projectName
            inceptionYear '2017'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://gitlab.intranet.opt/opt-core/opt-logging.git'
                connection 'scm:git@gitlab.intranet.opt:opt-core/opt-logging.git'
                developerConnection 'scm:git@gitlab.intranet.opt:opt-core/opt-logging.git'
            }
        }
    }.writeTo("build/libs/${projectName}-${version}.pom")
}

publishing {
    publications {
        testing(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}
